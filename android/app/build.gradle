plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = new Properties()
   def keystorePropertiesFile = rootProject.file('key.properties')
   if (keystorePropertiesFile.exists()) {
       keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
   }

android {
    namespace = "dev.silentpayments.danawallet"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

    defaultConfig {
        applicationId = "dev.silentpayments.danawallet"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        resValue "string", "app_name", "Dana"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'default'
    productFlavors {
        dev {
            dimension 'default'
            applicationIdSuffix '.dev'
            versionNameSuffix '.dev'
            resValue "string", "app_name", "Dana Development"
        }
        local {
            dimension 'default'
            applicationIdSuffix '.local'
            versionNameSuffix '.local'
            resValue "string", "app_name", "Dana Local"
        }
        signet {
            dimension 'default'
            applicationIdSuffix '.signet'
            versionNameSuffix '.signet'
            resValue "string", "app_name", "Dana Signet"
        }
        live {
            dimension 'default'
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation 'com.google.errorprone:error_prone_annotations:2.31.0'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
}
